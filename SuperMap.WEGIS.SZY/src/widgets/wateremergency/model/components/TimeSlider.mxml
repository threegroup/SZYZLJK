<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 creationComplete="creationCompleteHandler(event)">
	<fx:Metadata>
		[Event(name="change", type="sm.wegis.szy.events.TimeSliderEvent")]
	</fx:Metadata>
	<fx:Script>
		<![CDATA[
			import flash.utils.setTimeout;
			
			import mx.events.FlexEvent;
			
			import sm.wegis.szy.events.TimeSliderEvent;
			import sm.wegis.szy.models.ApplicationModelLocator;
			private var _data:Object;
			private var dataChange:Boolean = false;
			private var modelLocator:ApplicationModelLocator = ApplicationModelLocator.getInstance();
			
			private var isPlay:Boolean = false;
			private const interval:int = 2000;
			
			private var timer:Timer;
			
			[Bindable]
			private var currentValue:int = - 1;
			
			
			public function get data():Object
			{
				return _data;
			}
			
			public function set data(value:Object):void
			{
				_data = value;
				dataChange = true;
				invalidateProperties();
			}
			
			override protected function commitProperties():void
			{
				super.commitProperties();
				if (dataChange == true) {
					dataChange = false;
					resetPlay();
					if (data != null && data.currentPlan is Array &&  (data.currentPlan as Array).length > 0 
						&& (data.currentPlan as Array)[0]["result"] && (data.currentPlan as Array)[0]["result"] is Array){
						timeSlider.maximum = ((data.currentPlan as Array)[0]["result"] as Array).length -1;
						riverCompareType.enabled = timeSlider.enabled = dmStartBt.enabled = dmStopBt.enabled = true;
					}
				}
			}
			
			private function resetPlay():void
			{
				timeSlider.value = timeSlider.minimum;
				currentValue = -1;
				dmStartBt.label = "开始";
				isPlay = false;
				monLb.text = "";
				timeClear();
				timer.stop();
			}
			
			public function initParam():void
			{
				timeSlider.value = timeSlider.minimum;
				currentValue = -1;
				dmStartBt.label = "开始";
				isPlay = false;
				timer.stop();
				monLb.text = "";
				riverCompareType.enabled = timeSlider.enabled = dmStartBt.enabled = dmStopBt.enabled = false;
			}
			
			protected function play_clickHandler(event:MouseEvent):void
			{
				if (isPlay == false) {
					dmStartBt.label = "暂停";
					isPlay = true;
					timer.start();
				} else {
					dmStartBt.label = "开始";
					isPlay = false;
					timer.stop();
				}
			}
			
			private function playHandler(event:TimerEvent = null):void
			{
				if (isPlay == true && timeSlider.value <= timeSlider.maximum) {
					dispatchChangeEvent();
					if (timeSlider.value == timeSlider.maximum){
						setTimeout(resetPlay, interval);
					}
				}
			}
			
			private function dispatchChangeEvent(isPlay:Boolean = true):void
			{
				var event:TimeSliderEvent = new TimeSliderEvent(TimeSliderEvent.Change);
				if (isPlay == true)
				{
					currentValue += 1 ;
					if ( currentValue > timeSlider.maximum){
						resetPlay();
						return;
					}
				}
				timeSlider.value = currentValue;
				//月份索引
				var showData:Array = [];
				var pollutionItem:Object;
				var monthItem:Object;
				var chartDatas:Object = {};
				var label:String;
				//解析当前方案
				if (data.currentPlan != null){
					for each(pollutionItem in data.currentPlan) {
						//解析污染物
						if (pollutionItem.pollutionType == riverCompareBG.selectedValue){
							monthItem = pollutionItem.result[currentValue];
							//解析月份
							monLb.text = resolveMonth(monthItem.label);
							chartDatas.currentPlan = monthItem;
							break;
						}
					}
				}
				
				//解析对比方案
				if (data.comparePlan != null){
					for each(pollutionItem in data.comparePlan) {
						//解析污染物
						if (pollutionItem.pollutionType == riverCompareBG.selectedValue){
							//解析月份
							monthItem = pollutionItem.result[currentValue];
							chartDatas.comparePlan = monthItem;
							break;
						}
					}
				}
				event.data = chartDatas;
				dispatchEvent(event);
			}
			
			protected function stop_clickHandler(event:MouseEvent):void
			{
				resetPlay();
			}
			
			//播放结束，用于外部清除数据
			private function timeClear():void
			{
				var event:TimeSliderEvent = new TimeSliderEvent(TimeSliderEvent.Change);
				event.data = {};
				dispatchEvent(event);
			}
			
			protected function timeSlider_changeHandler(event:Event):void
			{
				currentValue = timeSlider.value;
				dispatchChangeEvent(false);
			}
			
			protected function riverCompareBG_changeHandler(event:Event):void
			{
				resetPlay();
			}
			
			//断面地形剖面数据
			private function dmProfileDataTipFunc(val:String):String
			{
				var month:String = "";
				if (data.currentPlan != null){
					for each(var pollutionItem:Object in data.currentPlan) {
						//解析污染物
						if (pollutionItem.pollutionType == riverCompareBG.selectedValue){
							var monthItem:Object = pollutionItem.result[timeSlider.value];
							//解析月份
							month = resolveMonth(monthItem.label);
							break;
						}
					}
				}
				return month;
			}
			
			private function resolveMonth(data:String):String
			{
				var month:String = "";
				if (data != null){
					var times:Array = data.split("-");
					if (times.length >= 2){
						month = times[1];
					}
				}
				return month;
			}
			
			private var isPlayerBeforeMouseDown:Boolean = false;
			protected function timeSlider_mouseDownHandler(event:MouseEvent):void
			{
				if (isPlay == true){
					isPlayerBeforeMouseDown = true;
					isPlay = false;
				}
			}
			
			protected function timeSlider_mouseUpHandler(event:MouseEvent):void
			{
				if (isPlayerBeforeMouseDown == true) {
					isPlayerBeforeMouseDown = false;
					isPlay = true;
					timer.reset();
					timer.start();
				}
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				timer = new Timer(interval);
				timer.addEventListener("timer", playHandler);
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<s:RadioButtonGroup id="riverCompareBG" change="riverCompareBG_changeHandler(event)"/>
	</fx:Declarations>
	<s:HGroup width="100%" height="100%" minHeight="35"  verticalAlign="middle" paddingRight="10" paddingLeft="10" bottom="5">
		<s:Label text="时间："/>	
		<s:Label id="monLb" height="100%" />
		<s:Label  text="月" height="100%"/>
		<s:HSlider id="timeSlider" enabled="false" width="100%" minimum="0" stepSize="1" snapInterval="1"
				   dataTipFormatFunction="dmProfileDataTipFunc"  
				   mouseDown="timeSlider_mouseDownHandler(event)"
				   mouseUp="timeSlider_mouseUpHandler(event)"
				   change="timeSlider_changeHandler(event)" />
		<s:HGroup id="riverCompareType">
			<s:Label text="污染物类型:" height="100%"/>
			<s:RadioButton id="NH3N" value="NH3-N" label="NH3-N"  groupName="riverCompareBG" selected="true"/>
			<s:RadioButton id="COD" value="COD" label="COD"  groupName="riverCompareBG"/>
		</s:HGroup>
		<s:Button id="dmStartBt" label="开始" enabled="false" buttonMode="true" styleName="SubmitRegisterStyle"
				  click="play_clickHandler(event)"/>
		<s:Button id="dmStopBt" label="停止" enabled="false" buttonMode="true" styleName="SubmitRegisterStyle"
				  click="stop_clickHandler(event)"/>
	</s:HGroup>
</s:Group>
