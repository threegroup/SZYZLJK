<?xml version="1.0" encoding="utf-8"?>
<ns:WidgetBase xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   xmlns:ns="library://ns.supermap.com/flex/wegis/common"
			   width="100%" height="100%"
			   creationComplete="initServiceLegend(event)">
	<fx:Script>
		<![CDATA[
			import com.adobe.cairngorm.control.CairngormEvent;
			import com.adobe.utils.StringUtil;
			import com.supermap.wegis.common.core.resourceManager.ResourceInfo;
			import com.supermap.wegis.common.core.resourceManager.ResourceManagerEx;
			
			import mx.collections.ArrayCollection;
			import mx.core.IVisualElementContainer;
			import mx.events.FlexEvent;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.http.HTTPService;
			
			import sm.wegis.szy.events.QueryEvent;
			import sm.wegis.szy.events.SystemEvent;
			import sm.wegis.szy.models.ApplicationModelLocator;
			import sm.wegis.szy.utils.CairngormEventClass;
			import sm.wegis.szy.utils.CairngormEventManager;
			import sm.wegis.szy.vo.ConstVO;
			import sm.wegis.szy.vo.WaterEvaluationVO;
			
			import spark.components.HGroup;
			
			[Bindable]
			private var modelLocator:ApplicationModelLocator  = ApplicationModelLocator.getInstance();
			public static const waterEvaluationState:String = "waterEvaluationState";
			public static const waterStandardState:String = "waterStandardState";
			public static const waterCountyState:String ="waterCountyState";
			
			/**保存动态图例前一次数据*/
			private var preDynaicLegendData:ArrayCollection; 
			private  const legendNameLen:int = 8;
			
			protected function initServiceLegend(event:FlexEvent):void
			{
				// TODO Auto-generated method stub
				var service:HTTPService= new HTTPService();   
				service.url = "config/waterEvaluationLegend.xml";   
				service.resultFormat="text";
				service.addEventListener(ResultEvent.RESULT, function(data:Object):void{
					var config:XML= XML(data.result.toString());
					analysisServiceXML(config);
				});   
				service.send(); 
				
				initEventListener();
				
				changeWaterEvaluationLegendHandler(null);
			}
			
			private function 	initEventListener():void
			{
				var eList:ArrayCollection = new ArrayCollection([
					[QueryEvent.Change_Water_Evaluation_Legend, changeWaterEvaluationLegendHandler],//评价河流
					[SystemEvent.SYSTEM_RELOAD, systemReloadHandler]
				]);
				var eventObject:CairngormEventClass = new CairngormEventClass();
				CairngormEventManager.getInstance().attachEvent(eventObject.encode(eList));
			}
			
			private function changeWaterEvaluationLegendHandler(event:CairngormEvent):void {
				var waterEvaluationVO:WaterEvaluationVO = modelLocator.waterEvaluationVO;
				var state:String = waterEvaluationState;
				switch(waterEvaluationVO.themeMapType) {
					case ConstVO.FUNCTION_STANDARD_THEME_TYPE:
					case ConstVO.FUNCTION_EVALUATION_THEME_TYPE:
						state= waterEvaluationState;
						break;
					case ConstVO.RIVER_WATER_EVALUATION_THEME_TYPE:
						state = waterStandardState;
						break;
					case ConstVO.COUNTY_THEME_TYPE:
						state = waterCountyState;
						break;
				}
				this.currentState = state;
			}
			
			private function systemReloadHandler(event:CairngormEvent):void {
				
			}
			
			
			private  function analysisServiceXML(xml:XML):void{
				//解析水质评价图例
				var xmlList:XMLList =  xml.waterEvaluation;
				if (xmlList.length() == 1){
					loadLegendIcon(xmlList[0], waterEvaluationLegendContainer);
				}
				
				//解析水质达标情况图例
				xmlList = xml.waterStandard;
				if (xmlList.length() == 1){
					loadLegendIcon(xmlList[0], waterStandardLegendContainer);
				}
				
				//解析行政区划--县图例
				xmlList = xml.waterCounty;
				if (xmlList.length() == 1){
					loadLegendIcon(xmlList[0], waterCountyLegendContainer);
				}
			}
			
			private function loadLegendIcon(xml:XML, parentContainer:IVisualElementContainer):void{
				var legendGroup:HGroup;
				var xmlList:XMLList =  xml.children();
				for each(var xmlItem:XML in xmlList)
				{
					var color:String = xmlItem.@color.toString();
					var legendName:String = xmlItem.@label.toString();
					if (legendGroup != null && legendGroup.numElements ==2) {
						parentContainer.addElement(legendGroup);
						legendGroup = new HGroup();
						legendGroup.percentWidth = 100;
					}
					var lengendItem:LegendComponent=new LegendComponent();
					lengendItem.percentWidth = 100;
					if (legendName.length > legendNameLen) {
						lengendItem.label.text =  legendName.substr(0,legendNameLen)+"...";
						lengendItem.toolTip =legendName;
					} else {
						lengendItem.label.text = legendName;
					}
					if (color != null ) {
						lengendItem.colorLb.setStyle("backgroundColor",parseInt(color,16));
					}
					if (legendGroup == null ) {
						legendGroup = new HGroup();
						legendGroup.percentWidth = 100;
					}
					legendGroup.addElement(lengendItem);
					if (xmlItem == xml.children()[xmlList.length() -1])
					{
						parentContainer.addElement(legendGroup);
					}
				}
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<fx:Component className="TitleComponent">
			<s:HGroup width="100%" horizontalAlign="center" verticalAlign="middle">
				<s:Label id="title" fontWeight="bold" fontSize="{getStyle('fontSize') + 1}"  verticalAlign="middle"/>
			</s:HGroup>
		</fx:Component>
		<fx:Component className="LegendComponent" >
			<s:HGroup gap="10" horizontalAlign="left" verticalAlign="middle" width="100%">
				<s:Label id="colorLb" width="25" height="13"/>
				<s:Label id="label"   textAlign="left"	 verticalAlign="middle"/>
			</s:HGroup>
		</fx:Component>
	</fx:Declarations>
	<ns:states>
		<!--水质评价图例-->
		<s:State name="waterEvaluationState"/>
		<!--水质达标图例-->
		<s:State name="waterStandardState"/>
		<!--行政区划图例-->
		<s:State name="waterCountyState"/>
	</ns:states>
	<s:Scroller width="100%" top="5" visible="false" visible.waterEvaluationState="true"
				bottom="3" horizontalScrollPolicy="off" verticalScrollPolicy="auto" >
		<s:Group  width="100%" height="100%" >
			<s:VGroup id="waterEvaluationLegendContainer" width="100%" height="100%" />
		</s:Group>
	</s:Scroller>
	<s:Scroller width="100%" top="5" visible="false" visible.waterStandardState="true"
				bottom="3" horizontalScrollPolicy="off" verticalScrollPolicy="auto" >
		<s:Group  width="100%" height="100%" >
			<s:VGroup id="waterStandardLegendContainer" width="100%" height="100%" />
		</s:Group>
	</s:Scroller>
	<s:Scroller width="100%" top="5" visible="false" visible.waterCountyState="true"
				bottom="3" horizontalScrollPolicy="off" verticalScrollPolicy="auto" >
		<s:Group  width="100%" height="100%" >
			<s:VGroup id="waterCountyLegendContainer" width="100%" height="100%" />
		</s:Group>
	</s:Scroller>
</ns:WidgetBase>
