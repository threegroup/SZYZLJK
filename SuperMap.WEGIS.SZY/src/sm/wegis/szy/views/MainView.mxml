<?xml version="1.0" encoding="utf-8"?>
<baseclass:ViewBase xmlns:fx="http://ns.adobe.com/mxml/2009" 
					xmlns:s="library://ns.adobe.com/flex/spark" 
					xmlns:mx="library://ns.adobe.com/flex/mx" 
					xmlns:baseclass="sm.wegis.szy.core.baseclass.*" 
					xmlns:ns="library://ns.supermap.com/flex/wegis/common"
					xmlns:supermap="http://www.supermap.com/iclient/2010"
					xmlns:basemap="widgets.basemap.*"
					width="100%" height="100%"
					creationComplete="creationCompleteHandler(event)">
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
		<s:Resize id="hideEffect" duration="200" widthFrom="300" widthTo="0" target="{BodyContainerID}" repeatCount="1"/>
		<s:Resize id="showEffect" duration="200" widthFrom="0" widthTo="300" target="{BodyContainerID}" repeatCount="1"/>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import com.adobe.cairngorm.control.CairngormEventDispatcher;
			import com.supermap.wegis.common.components.events.MenuEventEx;
			import com.supermap.wegis.common.components.menuBar.MenuItem;
			import com.supermap.wegis.common.core.resourceManager.ResourceManagerEx;
			import com.supermap.wegis.common.core.widget.WidgetManager;
			
			import mx.collections.ArrayCollection;
			import mx.core.IVisualElement;
			import mx.events.FlexEvent;
			
			import sm.wegis.szy.core.utils.MapCtrlTool;
			import sm.wegis.szy.events.MenuItemEvent;
			import sm.wegis.szy.events.SystemEvent;
			import sm.wegis.szy.skins.MenuItemSkinEx;
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				initListener();
			}
			
			public function initMehthod():void 
			{ 
				//初始化加载的widget 
				CairngormEventDispatcher.getInstance().addEventListener(SystemEvent.INITIAL_WIDGET 
					,addInitialWidget); 
				//根据菜单选择切换主体面板内容 
				CairngormEventDispatcher.getInstance().addEventListener(MenuItemEvent.SYSTEM_MENU_SELECT_CHANGE 
					,switchBodyContent); 
			} 
			
			/**初始化视图事件监听*/
			public function initListener():void
			{
				//系统菜单
				this.systemMenuId.addEventListener(MenuEventEx.MENU_CLICK, systemMenuHandler);
				
				//控制左侧面板显示隐藏
				this.ControlBodyControlID.addEventListener(MouseEvent.MOUSE_DOWN, controlBodyHandler);
			}
			
			//控制左侧面板显示隐藏
			private function controlBodyHandler(e:MouseEvent):void
			{
				this.showEffect.stop();
				this.hideEffect.stop();
				var tempWidth:Number = this.BodyContainerID.width;
				if(tempWidth == 0)
				{
					this.showEffect.play();
					this.ControlBodyControlID.source = ResourceManagerEx.FindResource("Left").cls;
				}
				else
				{
					this.hideEffect.play();
					this.ControlBodyControlID.source = ResourceManagerEx.FindResource("Right").cls;
				}
			}
			
			//系统一级菜单响应
			private function systemMenuHandler(e:MenuEventEx):void
			{
				if(e.Message is MenuItem)
				{
					var item:MenuItem = e.Message as MenuItem;
					var menuEvent:MenuItemEvent = new MenuItemEvent(MenuItemEvent.SYSTEM_MENU_SELECT_CHANGE);
					if(item != null)
					{
						menuEvent.data = item.data;
						menuEvent.dispatch();
					}
				}
			}
			
			//加载初始化widget
			private function addInitialWidget(event:SystemEvent):void
			{
				var initWidgetConfig:Object = event.data;
				if(initWidgetConfig == null)
				{
					return;
				}
				if(initWidgetConfig is ArrayCollection || initWidgetConfig is Array)
				{
					for each(var obj:Object in initWidgetConfig)
					{
						obj.parent = MainBodyContainerID;
					}
				}
				else if(initWidgetConfig is Object)
				{
					initWidgetConfig.parent = MainBodyContainerID;
				}
				WidgetManager.getInstance().loadWidget(initWidgetConfig);
			}
			
			
			//切换主体模块内容
			private function switchBodyContent(event:MenuItemEvent):void
			{
				var menuItemData:Object = event.data;
				if(menuItemData == null) {
					return;
				}
				
				this.BodyContainerID.removeAllElements();
				menuItemData.parent = this.BodyContainerID;
				WidgetManager.getInstance().loadWidget(menuItemData);
			}
			
			/*****添加二维地图控件******************************************************/
			public function addMapCtrl(component:IVisualElement):void
			{
				this.MapContainerID.addElement(component);
				
				this.initMapControl();
			}
			
			/**控件初始化，调用控件实例时，必须先调用该方法*/
			public function initMapControl():void
			{
				if(modelLocator.mapCtrl != null)
				{
					var mapCtrlTool:MapCtrlTool = new MapCtrlTool();
					mapCtrlTool.initMapCompass(this.MainBodyContainerID);
				}
			}
		]]>
	</fx:Script>
	<s:BorderContainer id="MapContainerID" width="100%" height="100%" backgroundColor="#FFFFFF">
	</s:BorderContainer>
	<s:VGroup width="100%" height="100%" gap="1">
		<s:BorderContainer id="TopContainerID"  width="100%" height="60">
			<s:HGroup width="100%" height="100%" paddingLeft="0" verticalAlign="middle" gap="0">
				<s:Rect  width="40" height="40">
					<s:fill>
						<s:BitmapFill source="{ResourceManagerEx.FindResource('logoIcon').cls}" fillMode="clip"/> 
					</s:fill>
				</s:Rect>
				<s:Rect width="345" height="45">
					<s:fill>
						<s:BitmapFill source="{ResourceManagerEx.FindResource('titleIcon').cls}" fillMode="clip"/> 
					</s:fill>
				</s:Rect>
				<s:Spacer width="100%"/>
				<ns:MenuBar id="systemMenuId" 
							height="100%"
							styleName="menuStyle"
							menuItemSkinClass="{MenuItemSkinEx}"
							isUpdateStatus="true"
							itemHorizontalGap="5" itemVerticalGap="4" 
							itemHeight="60" itemWidth="80"
							itemRowCount="1"
							isToolTip="false"
							dataProvider="{modelLocator.menuInfo.mapMenuList}"
							labelField="{modelLocator.menuInfo.mapMenuLabel}"/>
			</s:HGroup>
			<s:SWFLoader width="100%" height="100%" source="assets/swf/bubble.swf"/>
		</s:BorderContainer>
		<s:HGroup id="MiddleContainerID" width="100%" height="100%" paddingTop="0" paddingBottom="0" gap="0">
			<s:BorderContainer id="BodyContainerID"  width="300" height="100%">
				
			</s:BorderContainer>
			<s:Group id="MainBodyContainerID"  width="100%" height="100%">
				<s:BorderContainer left="0" top="0" backgroundAlpha="0.5">
					<s:VGroup width="100%" height="100%" verticalAlign="middle" horizontalAlign="center">
						<s:Image id="ControlBodyControlID"
								 source="{ResourceManagerEx.FindResource('Left').cls}"
								 buttonMode="true"/>
					</s:VGroup>
				</s:BorderContainer>
				<s:BorderContainer height="30" right="5" top="0">
					<ns:ToolBar id="MapTool" height="100%"
								buttonStyleName="toolStyle"
								dataProvider="{modelLocator.toolInfo.mapToolList}"
								labelField="{modelLocator.toolInfo.mapToolLabel}"/>
				</s:BorderContainer>
			</s:Group> 
		</s:HGroup>
		<s:BorderContainer id="BottomContainerID"  width="100%">
			<s:HGroup id="CopyRight" width="100%" height="100%" gap="0" horizontalAlign="left" 
					  verticalAlign="middle">
				<s:Label fontFamily="Times New Roman" text="©" verticalAlign="middle"/>
				<s:Label text="{modelLocator.systemInfo.copyRight}"  verticalAlign="middle"/>
			</s:HGroup>
		</s:BorderContainer>
	</s:VGroup>
</baseclass:ViewBase>
